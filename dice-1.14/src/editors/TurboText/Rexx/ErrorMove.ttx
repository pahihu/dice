/*
**      $Id: ErrorMove.ttx,v 30.0 1994/06/10 18:06:17 dice Exp $
**
**      DICE Error Parsing system.  Script for Oxxi TurboText (tm).
**
**  Notes: This assumes that your DCC:Config/DCC.Config file contains the
**         following line:
**
**  cmd= rx DCC:Rexx/TTX_ErrorParse.rexx %e "%c" "%f" "%0"
*/

PARSE UPPER ARG COMMAND  /* Valid commands: Current First Next Prev */

OPTIONS RESULTS

IF ~show('p','DICE_ERROR_PARSER') THEN
   DO
      'SetStatusBar Error Parsing Not Active'
      EXIT 0
   END

ADDRESS DICE_ERROR_PARSER COMMAND E
IF rc ~= 0 THEN
   DO
      'SetStatusBar No More Errors'
      exit 0
   END

IF E.LINE = 0 THEN
   DO
   IF LEFT(E.TEXT, 5) = 'DLINK' THEN
      DO
         TT = TRANSLATE(E.STRING, '-', '"')
         /* This is a DLINK error, we need to handle it special */
         'RequestBool "There were DLINK Errors" PROMPT "'TT'"'
         exit 0
      END
   END

ADDRESS DICE_ERROR_PARSER TTXSAME '"'E.FPATH'" "'E.ARGS'"'
IF RC ~= 0 THEN
   DO
      'SetStatusBar Unable to open' E.FPATH
      exit 0
   END

Port = RESULT
ADDRESS Value Port
'MoveBookMark' 1000+E.line
IF RC = 0 THEN
DO
   'GetCursorPos'
   E.Line = word(result, 1)
END
   'Move FOLDS' E.Line E.Col

'SetStatusBar' E.STRING
